
% simple implementation of SOUNDEX

% elflib.SOUNDEX:<soundex word> = soundex-code-for-word

#uses Implode Explode Rest.

#vars $soundexHW: h 'H' w 'W'.
#vars $soundexClass1: b 'B' f 'F' p 'P' v 'V'.
#vars $soundexClass2: c 'C' g 'G' j 'J' k 'K' q 'Q' s 'S' x 'X' z 'Z'.
#vars $soundexClass3: d 'D' t 'T'.
#vars $soundexClass4: l 'L'.
#vars $soundexClass5: m 'M' n 'N'.
#vars $soundexClass6: r 'R'.
#vars $soundexClass: 1 2 3 4 5 6.


#hide elflib.SOUNDEX.
elflib.SOUNDEX:
	<> ==
	<$any> == elflib.SOUNDEX1:<soundex $any.> <>
.

#hide elflib.SOUNDEX1.
elflib.SOUNDEX1:
	<> == elf.TOP
	<soundex $stem> == <soundex2 Explode:<$stem .> .> 
	<soundex2 $a> == Implode:<<pad $a <remove <contract <replace $a> .> .> .> .>
	
	<replace $soundexClass1> == 1 <replace>
	<replace $soundexClass2> == 2 <replace>
	<replace $soundexClass3> == 3 <replace>
	<replace $soundexClass4> == 4 <replace>
	<replace $soundexClass5> == 5 <replace>
	<replace $soundexClass6> == 6 <replace>
	<replace> == <replace2>
	<replace2 $any> == $any <replace>
	<replace2> ==
	
	<contract $soundexClass $soundexClass> == <contract $soundexClass> 
	<contract $soundexClass $soundexHW $soundexClass> == <contract $soundexClass> 
	<contract $a> == $a <contract>
	<contract> ==
	
	<remove $a> == $a <remove2>
	<remove2 $soundexClass> == $soundexClass <remove2>
	<remove2> == <remove3>
	<remove3 $any> == <remove2>
	<remove3> ==
	
	<pad $a $w $x $y $z> == $a $x $y $z
	<pad $a $w $x $y> == $a $x $y 0
	<pad $a $w $x> == $a $x 0 0
	<pad $a $w> == $a 0 0 0
.