
% firstname support - see also list of firstnames in words/firstname.dtr

nimrodel.firstname.FIRSTNAME-LOOKUP:
	<> == **fail**
	<$nimrodel-firstnameword-male> == 'nimrodel.firstname.MALEFIRSTNAMEROOT'
	<$nimrodel-firstnameword-female> == 'nimrodel.firstname.FEMALEFIRSTNAMEROOT'
.

nimrodel.firstname.FIRSTNAMEROOT:	
	<> == nimrodel.LEXROOT:<>
	
	<token type> == 'firstname'
	<token gender> == unknown
	<lex type> == 'Person'
	<lex cat> == propn
.

nimrodel.firstname.TRYFIRSTNAME:	
	<> == elf.TOP
	<$$n> == Local
	
	<$$n lex> == <$$n try phrase lex>
	
	<$$n try phrase> == <$$n <map nimrodel.context.NEXT:<2 token context .> .> >
	
	<map son of> == try type 2 firstname
	<map de> == try type 1 placename 
	<map of> == try type 1 placename 
	<map his brother> == case brother
	<map the> == try type 1 occupation
	<map le> == try type 1 occupation
	<map> == try type 0 surname
	
	<$$n try type $$skip $$target> == <$$n <map2 $$target <$$n here nimrodel.context.SKIP:<$$skip token.> token next token type .> .> >
	
	<map2 firstname firstname> == case skip 3 lex
	<map2 firstname propnoun> == case skip 3 lex
	<map2 placename placename> == case skip 2 lex
	<map2 placename propnoun> == case skip 2 lex
	<map2 placename2 placename> == case skip 3 lex
	<map2 placename2 propnoun> == case skip 3 lex
	<map2 placename2> == case skip 1 lex
	<map2 surname surname> == try surname
	<map2 surname propnoun> == try surname
	<map2 surname 'Occupation'> == case skip 1 lex
	<map2 occupation 'Occupation'> == case skip 2 lex
	<map2 occupation> == case skip 2 lex
	<map2> == case default 
	
	<$$n try surname> == <$$n <map3 <$$n here token next token next token stem .> .> >
	
	<map3 de> == try type 2 placename2
	<map3 of> == try type 2 placename2
	<map3> == case skip 1 lex
	
	<$$n case skip $$m> == nimrodel.phraseutils.SKIP:< $$m 'nimrodel.firstname.FIRSTNAMEROOT'>
	<$$n case brother> == nimrodel.phraseutils.SKIP:< 2 'nimrodel.firstname.FIRSTNAMEROOT' lex>
	<$$n case default> == Local:<$$n>
.

nimrodel.firstname.FEMALEFIRSTNAMEROOT:
	<> == nimrodel.firstname.TRYFIRSTNAME:<'nimrodel.firstname.FIRSTNAMEROOT'>
	<token gender> == female
.

nimrodel.firstname.MALEFIRSTNAMEROOT:
	<> == nimrodel.firstname.TRYFIRSTNAME:<'nimrodel.firstname.FIRSTNAMEROOT'>
	<token gender> == male
.


