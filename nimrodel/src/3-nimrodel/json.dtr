% vim: set noexpandtab:

% outputting JSON annotations for people only

% note: type signatures should be treated informal only
% keeping datr's stream processing model in mine

#uses Gensym Length Implode Explode LocalPath Rest.
#vars $eq: '=' '=='.

% ---------------------------------------------------------------------
% glossary
% ========
%
% nimrodel literal: sequence of tokens that correspond to
%     a single fragment of nimrodel's internal semantic
%     representation
%
%     [ supertype type start-char end-char etc... ]
%
%     Syntactically, the nimrodel internal semantic representation
%     is nested/recursive; although it is flat in essence.
%
%     Literals for entities are completely flat, and we will try
%     to refer to these as entity literals
%
% named line: a line of the form <prefix> <entity literal> <nl>
%     the payload is some arbitrary token sequence
%     (here an entity literal)
%
%     the prefix serves as an identifier for the literal
%
% tagged bundle: a handle followed by a list of named lines.
%      the handle is the prefix of one of the named lines;
%
%      It says in essence that while the bundle represents
%      several entities, one of them is of particular interest
%      to us
%
% distinguished entity: in a tagged bundle, the entity that
%      is being pointed to by the bundle handle
% ---------------------------------------------------------------------


app.JSON:
	<> == elf.TOP
	<sp> == ' '
	<tab> == '\t'
	<nl> == '\n'
	<json reset> == 
		Gensym:<reset 'T' 'R' 'E' 'A' 'N' '#'>	
	
	% ignore document title
	<json title> ==
	
	<json sentence> == <convert
		<select entity 'Person' <named strip
			%Debug:<tracemsg '----v--sentence--v----\n'
			<json sentence-next>
			%.>
		.> .> .>
		<nl>
	<json sentence-next $documentString [ > ==
		<json clause $documentString <next> ] .>
		<json sentence-next $documentString <skip> .>
	<json sentence-next> ==	
	
	<json clause $documentString entity **endpoint** > ==
	<json clause $documentString> ==
		%Debug:<writeline Implode:<'CLAUSE ' [ app.DUMP:<pretty> .>.>
		%Debug:<trace
		<tail <json clause2 $documentString > .>
		%.>
	
	<json clause2 $documentString entity> == <json entity Gensym:<gensym 'T'.> $documentString>
	<json clause2 $documentString rel> == <json rel Gensym:<gensym 'R' .> $documentString>
	<json clause2 $documentString rel $f equals> == <json rel equals $documentString $f equals>
	<json clause2 $documentString attr> == <json attr Gensym:<gensym 'A' .> $documentString>
	<json clause2 $documentString property> == <json property $documentString>
	<json clause2> == <json error clause-error>
	
	<json entity $id $documentString> ==
		<json entity2 <json check> $id $documentString>
	<json entity2 ok $id $documentString $type $start-char $end-char> ==	
		<json render-entity $documentString $id $type $start-char $end-char>
	<json entity2 skip $id> == $id
	<json entity2> == <json error> 
	
	<json check> == ok
	<json check **endpoint**> == skip
	<json check **fail**> == type-error
	<json check $any **fail**> == range-error
	<json check $any $any2 **fail**> == range-error

	<json error clause-error $documentString> == 
		<json render-error 'T1' <implode-unwords
			'unknown clause: ' <next>  .>>
	<json error type-error $id $documentString $type $start-char $end-char> ==
		<json render-error $id <implode-unwords
			'No type specified'
			$start-char $end-char
			( <text-for-span $documentString $type $start-char $end-char .> )
			<next> .> >
	<json error range-error $id $documentString $type $start-char $end-char> ==
		<json render-error $id <implode-unwords
			'Range error:' $type $start-char $end-char
			<next> .> >
	<implode-unwords> == Implode:<app.DUMP:<unwords> .>

	% render-error : string -> string -> tagged bundle
	<json render-error $id $msg> ==
		$id $id [ entity error 0 0 error '=' $msg ] <nl .>

	% ---------------------------------------------------------------------
	% Relations are tricky.
	%
	% The input is more or less a (recursive) nimrodel literal, although
	% abstracting from the detail, we can think of the input as being
	% fundementally `Rel(focus, side1, side2)`, where focus is either
	% 1 or % 2 referring to which side the relation is about.
	%
	% It may be useful to keep a few key points in mind:
	%
	% 1. in essence, a relation literal actually refers to a single
	%    entity, ie. the one represented by side1 or side2
	%
	% 2. in form, a relation literal is a nested representation for
	%    several entities, so we don't just emit one entity but a
	%    whole list of them as output
	%
	% 3. indeed the same is true for side1 and side2; while they
	%    are in essence referring to a single entity, they return
	%    tagged bundles (see glossary) along the way which we want
	%    to merge and emit
	%
	% We manage this by using a tagged bundle representation for
	% our output: ie. a list of named lines (multiple entities),
	% along with a handle saying which one we are referring to.
	% This allows us to manipulate the individual entities referred
	% to by side1 or side2 (we scurry up and down the named lines
	% looking for the entity referred to by the bundle handle).
	% This may make a bit more sense after some (hopefully now less)
	% squinting at the code.
	% ---------------------------------------------------------------------


	<json rel $id $documentString $focus $type [ > == 
		%Debug:<writeline '--- vvv rel ' $id $type 'vvv ---' .>
		%Debug:<trace
		<json rel-inner $id $documentString $focus $type
		        <json clause2 $documentString <next> ] .> ] <skip> .>
		%.> % trace
		%Debug:<writeline '--- ^^^ rel ' $id $type '^^^ ---' .>

	<json rel-inner $id $documentString $focus equals $arg1> ==
                <json rel-equals $id $documentString $focus equals $arg1>
	<json rel-inner $id $documentString $focus is_widow_of $arg1> ==
		<json rel-inject 'lateHusband' $id $documentString $focus is_widow_of $arg1>
	<json rel-inner> == <json rel-generic>

	% json rel-generic: relation literal (ish) -> tagged bundle
	% render both sides and do nothing else
	<json rel-generic $id $documentString $focus $type $arg1> ==
		<json focus $focus $arg1 			% target id for clause
			<next>	]		% annotation for first operand
			<json rel-next $id $documentString $type $arg1 <skip> .>
		.>
	% json rel-equals: relation literal (ish) -> tagged bundle
	% merge the distinguished entity in the two sides
	% (other lines are left intact)
	<json rel-equals $id $documentString $focus $type $arg1> ==
		<json merge-id $arg1
			<json clause2 $documentString <tail <skip> .> .>
			<next>
		.>
	% json rel-inject: string -> relation literal (ish) -> tagged bundle
	% in addition to the usual, inject the origOccurrence feature
	% into the focused argument as the target feature name
	<json rel-inject $attr-tgt $id $documentString $focus $type $arg1> ==
		<named inject 'origOccurrence' $attr-tgt
			<json endpoints $focus $arg1	% target id for clause
				<next>	]		% annotation for first operand
				<json rel-next $id $documentString $type $arg1 <skip> .>
			.>
		.>

	% json rel-next: produce annotation for second operand
	<json rel-next $id $documentString $type $arg1 [ > ==
		<json rel-next2 $id $type $arg1 <json clause2 $documentString> .>
	<json rel-next2 $id $type $arg1 $arg2> ==
		$arg2
		LocalPath:<> 	% annotation for second operand

	% json focus: focus -> ??? -> tagged bundle
	% (here it's not a list of lines per se that's the input, but
	% something close to it)
	% prefix our output with the id of the focused line
	<json focus 1 $arg1> == $arg1 <next> <json focus3 <skip> .>
	<json focus 2 $arg1> == <json focus2 <skip> .> <next> <json focus3 <skip> .>
	<json focus2 $arg2 > == $arg2
	<json focus3 $arg2 > == LocalPath:<>

	% json endpoints: focus -> ??? -> tagged bundle
	% almost identical to json focus, only we prefix with
	% the less prefered one, and then the handle
	%
	% (so json focus should be a suffix of this)
	<json endpoints 1 $arg1> ==
		<named handle <skip> .> $arg1
		<next> <json endpoints3 <skip> .>
	<json endpoints 2 $arg1> ==
		$arg1 <named handle <skip> .>
		<next> <json endpoints3 <skip> .>
	<json endpoints2 $arg2 > == $arg2
	<json endpoints3 $arg2 > == LocalPath:<>


	% rewrite the block replacing both the $$arg1 and the $$arg2 line with
	% a merged equivalent
	<json merge-id $$arg1 $$arg2> ==
		$$arg1
		$$arg1 <literal merge
			<named strip-takeline $$arg1>
			<named strip-takeline $$arg2> .> <nl>
		Rest:< <json merge-id2 $$arg1 $$arg2 '\n' > .>
	<json merge-id2 $$arg1 $$arg2 '\n' $$arg1> ==
		<json merge-id2 $$arg1 $$arg2 <skipline> .>
	<json merge-id2 $$arg1 $$arg2 '\n' $$arg2> ==
		<json merge-id2 $$arg1 $$arg2 <skipline> .>
	% this is just straight token rewriting s/$$arg2/$$arg1/
	<json merge-id2 $$arg1 $$arg2 $$arg2> ==
		$$arg1 <json merge-id3 $$arg1 $$arg2>
	<json merge-id2> == <json merge-id3>
	<json merge-id3 $$arg1 $$arg2 $$any> ==
		$$any <json merge-id2 $$arg1 $$arg2>
	<json merge-id3> ==

	% --------------------------------------------------
	% entities
	% --------------------------------------------------

	% json render-entity : ??? -> tagged bundle
	% slightly modify entity by adding an origOccurrence attribute
	<json render-entity $documentString $id $type $start-char $end-char> ==
		$id $id
		[ entity $type $start-char $end-char
			origOccurrence '='
			<text-for-span $documentString $type $start-char $end-char .>
			<next>% take till closing bracket (ignores multiples)
		] <nl .>

	% --------------------------------------------------
	% properties and attributes: insert an attribute/value
	% pair into the literal for the embedded entity
	% --------------------------------------------------

	<json attr $id $documentString $attr $val [ > ==
		<json attr2 $id $documentString $attr $val <json clause2 $documentString> .>
	<json attr2 $id $documentString $attr $val $arg> ==
		$arg
		<named add-feature $arg $attr $val>
		
	<json property $documentString $attr $val [ > ==
		<json property2 $documentString $attr $val <json clause2 $documentString> .>
	<json property2 $documentString $attr $val $arg> ==
		$arg
		<named add-feature $arg $attr $val>

	% --------------------------------------------------
	% post-processing after tree rewriting and before
	% converting to json
	% --------------------------------------------------

	% select: string -> string -> [literal] -> [literal]
	% pick out only literals of the given type and subtype
	% eg. <select entity "Person">
	<select $supertype $type [> ==
		<select-one $supertype $type [ <next> ] .>
		<select $supertype $type <skip> .>
	<select> ==
		
	<select-one $supertype $type [ $supertype $type> ==
		[ $supertype $type LocalPath:<>
	% always propagate errors, no matter what
	<select-one $supertype $type [ $supertype error> ==
		[ $supertype error LocalPath:<>
	<select-one $supertype $type> ==

	% --------------------------------------------------
	% nimrodel to json conversion
	% --------------------------------------------------

	% convert flat list of nimrodel internal semantic literals to
	% list of json records; we ignore types and assume that each
	% feature in each literal is meant to serve as a dictionary key
	<convert> ==
		<mk-json long-list <convert-helper> .>
	<convert-helper [> ==
		<convert-one <next> ] .>
		<convert-helper <skip> .>
	<convert-helper> ==	
	<convert-one entity $any $start-char $end-char> ==
		<mk-json record json.UNIQUE:<unique <strip-eq> .> .>

	% from [ f1 = v2 f2 =v3 ] to just [ f1 v2 f2 v3 ]
	<strip-eq $$f $eq $$v> == $$f $$v <strip-eq>
	<strip-eq ]> ==
	<strip-eq> == <strip-eq2>
	<strip-eq2 $any> ==
		'error'
		<implode-unwords 'was expecting only sequence of f = v, but got' $any>
	<strip-eq2> ==

	<mk-json> == json.STRUCT:<>

	% --------------------------------------------------
	% low-level nimrodel token stream or literal stuff
	% --------------------------------------------------

	<named> == nimrodel.NAMED:<>
	<literal> == nimrodel.LITERAL:<>
	<stream> == nimrodel.STREAM:<>

	<init> == <stream init>
	<tail> == <stream tail>
	<takeline> == <stream takeline>
	<skipline> == <stream skipline>
	<next> == <stream next>
	<skip> == <stream skip>

	<skipb [> == <stream skip>
	<nextb [ > == <steram next>

	% --------------------------------------------------
	% working with the document text
	% --------------------------------------------------

	<text-for-span $documentString $type $start-char $end-char> ==
		openNLP.GetSpanStrings:<$documentString
			openNLP.NewSpan:<$type $start-char $end-char .> .>

.


% nimrodel named lines
nimrodel.NAMED:
	% inject :  string -> string -> string -> string
	%        -> [namedline] -> [namedline]
	%
	% given attr-src attr-tgt arg-src arg-tgt [named line]
	% lift the source feature from the source line and
	% inject it into the target line
	<inject $attr-src $attr-tgt $arg-src $arg-tgt> ==
		$arg-tgt
		<add-feature $arg-tgt
			$attr-tgt
			<literal get-attr $attr-src
				<takeline $arg-src> .>>

	% named add-feature $named $key $val
	%
	% add the feature to the line prefixed with $named
	% pass the rest of lines through
	<add-feature $arg $key $value $any> ==
		<add-feature-one $arg $key $value $any>
		<add-feature $arg $key $value <tail <stream skipline> .> .>
	<add-feature> ==
	% helper: what to do when we actually hit the line
	<add-feature-one $arg $key $value $arg> ==
		$arg <literal add-feature $key $value <stream takeline> .> <nl>
	<add-feature-one> == <add-feature-one2>
	<add-feature-one2 $arg $key $value $any> ==
		$any <stream takeline> <nl> % pass through

	% strip : [namedline] -> [literal]
	<strip $id [> ==
		[ <stream takeline> <strip <tail <stream skipline> .> .>
	<strip> ==

	% handle: namedline -> string
	% take the handle associated with a fragmentary output
	<handle $any> == $any

	% strip-takeline : string -> [namedline] -> flat_literal
	<strip-takeline> == <tail <takeline> .>

	% takeline : string -> [namedline] -> named line
	% given a block of named lines, select the line prefixed with $name
	<takeline $arg $arg> == $arg <stream takeline>
	<takeline> == <takeline2>
	<takeline2 $arg $any> == <takeline $arg
		<tail <stream skipline> .>
	.>
	<takeline2> ==

	<literal> == nimrodel.LITERAL:<>
	<stream> == nimrodel.STREAM:<>
	<tail> == <stream tail>
	<nl> == '\n'
.
